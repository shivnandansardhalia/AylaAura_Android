apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "com.aylanetworks.aura"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 49
        versionName "5.6.02"
        multiDexEnabled true
    }
    signingConfigs {
        release {
            try {
                storeFile file(System.getenv("KEYSTORE"))
            } catch (all) {
            }
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
        debug {
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
            storeFile rootProject.file("release/store/debug.keystore")
        }
    }

    productFlavors {
        aGoogle {
        }
        bBaiduPush {
        }
    }

    sourceSets{
        bBaiduPush {
            jniLibs.srcDirs = ['libs']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    bBaiduPushCompile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile project(':aylasdk')
    compile project(':websocketLibrary')
    compile 'com.google.android.gms:play-services-gcm:10.2.0'
    compile 'com.google.android.gms:play-services-auth:10.2.0'
    compile 'com.android.support:recyclerview-v7:25.3.0'
    compile 'com.android.support:cardview-v7:25.3.0'
    compile 'org.honorato.multistatetogglebutton:multistatetogglebutton:0.2.1'
    compile 'com.android.support:support-v4:25.3.0'
    testCompile 'junit:junit:4.12'
}

android.applicationVariants.all { variant ->

    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"

        //path to resolve type references
        classpath = files(variant.javaCompile.classpath.files, project.android.getBootClasspath()) + files(ext.androidJar)

        //javadoc options
        options.setWindowTitle("Ayla Aura")

        //exclude patterns
        exclude '**/BuildConfig.java', '**/R.java'
    }
}
